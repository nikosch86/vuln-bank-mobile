apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

react {
    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = false
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

// Get security level from environment variable
def securityLevel = System.getenv("SECURITY_LEVEL") ?: "none"

// Map security levels to suffixes and app names (neutral naming for candidates)
def securityConfig = [
    "none":            [suffix: ".l0", appName: "VulnBank L0"],
    "library":         [suffix: ".l1", appName: "VulnBank L1"],
    "proxy-bypass":    [suffix: ".l2", appName: "VulnBank L2"],
    "custom":          [suffix: ".l3", appName: "VulnBank L3"],
    "frida-resistant": [suffix: ".l4", appName: "VulnBank L4"],
]

def config = securityConfig[securityLevel] ?: securityConfig["none"]

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.vulnerablebankapp"

    defaultConfig {
        applicationId "com.vulnerablebankapp${config.suffix}"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        // Set app name based on security level
        resValue "string", "app_name", config.appName
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
